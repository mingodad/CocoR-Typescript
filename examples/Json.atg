COMPILER Json

CHARACTERS
    digit    = "0123456789".
    digitH   = "123456789".
    hexCh    = "0123456789ABCDEFabcdef".
    cr       = '\r'.
    lf       = '\n'.
    tab      = '\t'.
    stringCh = ANY - '"' - '\\' - cr - lf.
    escCh    = "\"\\/bfnrt".

TOKENS
    number = [ '-' ] ('0' | digitH { digit }) [ '.' { digit } ] [ ('e' | 'E') [ '+' | '-' ] digit { digit } ].
    string = '"' { stringCh | '\\' escCh | "\\u" hexCh hexCh hexCh hexCh } '"'.

IGNORE cr + lf + tab

/*-------------------------------------------------------------------------*/

PRODUCTIONS

Json = XObject.

XObject = '{' [ KVPairList ] '}'.

KVPairList = KVPair { ',' KVPair }.

KVPair = string ':' Value.

XArray = '[' [ ValueList ] ']'.

ValueList = Value { ',' Value }.

Value = string | number | XObject | XArray | "true" | "false" | "null".

END Json.
